plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/mysticalagriculture.accesswidener")
	runs {
		//
		// This adds a new gradle task that runs the datagen API: "gradlew runDatagenClient"
		//
		datagenClient {
			inherit client
			name "Data Generation"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
			vmArg "-Dfabric-api.datagen.modid=${modid}"

			runDir "build/datagen"
		}
	}
}

repositories {
	maven { url = "https://mvn.devos.one/snapshots/" }
	mavenLocal()

	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
        name = "Shedaniel"
        url = "https://maven.shedaniel.me/"
    }

	maven {
		name = "Jitpack"
		url = 'https://jitpack.io'
	}

	maven {
		name = "Curse"
		url = "https://www.cursemaven.com"
	}

	maven {
		// location of the maven that hosts JEI files before January 2023
		name = "Progwml6's maven"
		url = "https://dvs1.progwml6.com/files/maven/"
	}

	maven {
		name = "BlameJared Maven"
		url = "https://maven.blamejared.com"
	}

	maven {
		// location of a maven mirror for JEI files, as a fallback
		name = "ModMaven"
		url = "https://modmaven.dev"
	}

	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}

	maven { url = "https://mvn.devos.one/snapshots/" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	//mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.

	if (project.runtime_itemlist_mod == "jei") {
		modImplementation("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}") {
			exclude group: "mezz.jei"
		}
	} else {
		modCompileOnly("mezz.jei:jei-${project.minecraft_version}-fabric:${project.jei_version}") {
			exclude group: "mezz.jei"
		}
	}

	if (project.runtime_itemlist_mod == "rei") {
		// Manually override architectury TODO remove once REI's dependency is fixed
		modRuntimeOnly "dev.architectury:architectury-fabric:6.5.69"
		modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
			exclude(group: "net.fabricmc.fabric-api")
		}
	} else {
		modCompileOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
			exclude group: "net.fabricmc.fabric-api"
		}
	}

	if (!(project.runtime_itemlist_mod in ["jei", "rei"])) {
		throw new GradleException("Invalid runtime_itemlist_mod value: " + project.runtime_itemlist_mod)
	}
	
	include modImplementation ("com.github.Draylar:magna:${project.magna_version}") {
        exclude group: "net.fabricmc"
        exclude group: "com.jamieswhiteshirt"
        exclude group: "me.shedaniel.cloth"
		exclude group: "io.github.prospector", module: "modmenu"
	}

	modImplementation "vazkii.patchouli:Patchouli:${project.patchouli_version}"

	include modImplementation("vazkii.patchouli:Patchouli:${project.patchouli_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modImplementation "curse.maven:jade-324717:${project.jade_version}"

	modImplementation("com.alex.cucumber:Cucumber-Refabricated:1.0.6")

	implementation "com.electronwill.night-config:core:${project.nightconfig_version}"
	implementation "com.electronwill.night-config:toml:${project.nightconfig_version}"
	modApi "net.minecraftforge:forgeconfigapiport-fabric:4.2.11"

	modImplementation(include("io.github.fabricators_of_create.Porting-Lib:Porting-Lib:${project.portinglib_version}"))
}

base {
	archivesName = project.archives_base_name+"-"+project.minecraft_version
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/generated/resources'
			]
		}
	}
}

configurations.all {
	resolutionStrategy {
		force("net.fabricmc:fabric-loader:$loader_version")
	}
}